#!/bin/gawk
# Initialize variables

BEGIN {
    # State tracking
    in_doc = 0
    current_section = ""

    # Documentation parts
    func_name = ""
    doc_text = ""
    param_text = ""
    return_text = ""
    print "<h3>" file "</h3>"
    print "<ul>"
}

# Start of JSDoc comment
/^\/\*\*/ {
    in_doc = 1
    func_name = ""
    doc_text = ""
    param_text = ""
    return_text = ""
    is_deprecated = 0
    next
}

# End of JSDoc comment
in_doc && /^\s*\*\// {
    in_doc = 0
    next
}

# Process JSDoc content
in_doc {
    # Remove leading * and spaces
    sub(/^\s*\* ?/, "", $0)

    if ($1 == "@deprecated") {
        is_deprecated = 1
        next
    }

    # Handle @param tag
    if ($1 == "@param") {
        # Try to match with type first


        if ($2 ~ /^{/) {
            param_type_end = 0
            for (i = 2; i < NF; i++) {
                if ($i ~ /}$/) {
                    param_type_end = i
                    break
                }
            }

            param_type = ""
            sub(/{/, "", $2)
            sub(/}/, "", $param_type_end)
            for (i = 2; i <= param_type_end; i++) {
                sub(/</, "\\&lt;", $i)
                param_type = param_type " " $i
            }
            param_name_pos = param_type_end + 1
            param_text = param_text "<span class=\"parameter-item\">"
            param_text = param_text "<span class=\"parameter-name\"><code>" $param_name_pos "</code></span>"
            param_text = param_text "<span class=\"parameter-type\">(" param_type ")</span>"
            desc = ""
            for (i = 4 + param_type_end - 2; i <= NF; i++) {
                desc = desc " " $i
            }
            param_text = param_text "<span class=\"parameter-description\">" desc "</span>"
            param_text = param_text "</span>"
        } else {
            param_text = param_text "<span class=\"parameter-item\">"
            param_text = param_text "<span class=\"parameter-name\"><code>" $2 "</code></span>"
            desc = ""
            for (i = 3; i <= NF; i++) {
                desc = desc " " $i
            }
            param_text = param_text "<span class=\"parameter-description\">" desc "</span>"
            param_text = param_text "</span>"
        }

        next
    }

    # Handle @returns tag
    if ($0 ~ /^@returns/) {
        match($0, /@returns\s+(.+)/, arr)
        if (arr[1] != "") {
            return_text = arr[1]
        }
        next
    }

    # Collect main documentation text
    doc_text = doc_text $0 "<BR>"
}

# Process function declaration
!in_doc && /^(async\s+)?function/ {
    match($0, /function\s+([a-zA-Z0-9_]+)/, arr)
    if (arr[1] != "") {
        func_name = arr[1]

        private = func_name ~ /^_/
                                           #UI is a special case
        unstable = !(func_name ~ /.+_/) && !(func_name ~ /UI$/)

        # Print function documentation
        print "<article class=\"doc-card\">"

        classList = "function-name"
        if (private) classList = classList " private"
        if (unstable && !private) classList = classList " unstable"
        if (is_deprecated) classList = classList " deprecated"

        print "<p class=\"" classList "\"><code>" func_name "</code></p>"

        if (doc_text != "") {
            print "<p class=\"function-description\">" doc_text "</p>"
        }

        # Print parameters if any
        if (param_text != "") {
            print "<section class=\"parameters\">"
            print "<h4>Parameters</h4>"
            print "<span class=\"parameter-list\">" param_text "</span>"
            print "</section>"
        }

        # Print return type if any
        if (return_text != "") {
            sub(/</, "\\&lt;", return_text)
            print "<section class=\"return\">"
            print "<h4>Returns</h4>"
            print "<span class=\"return-type\">" return_text "</span>"
            print "</section>"
        }

        print "</article>"

    }

    #reset state
    func_name = ""
    doc_text = ""
    param_text = ""
    return_text = ""
    is_deprecated = 0
}

# Print footer
END {
    print "</ul>"
}
